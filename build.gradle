plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "8.1.0"
}

group = 'me.eyetealer.mckingofthehill'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    // Paper
    compileOnly "io.papermc.paper:paper-api:${project.papermcVersion}"

    // Database connectors
    implementation "com.mysql:mysql-connector-j:${project.mysqlConnectorJ}"
    implementation "org.mariadb.jdbc:mariadb-java-client:${project.mariadbJavaClient}"
    implementation "org.postgresql:postgresql:${project.postgresql}"

    // Cloud command framework
    implementation "cloud.commandframework:cloud-paper:${project.cloudCommandframework}"
    implementation "cloud.commandframework:cloud-annotations:${project.cloudCommandframework}"

    // Testing frameworks
    testImplementation "org.junit.jupiter:junit-jupiter:${project.junitJupiterVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${project.mockitoJunitJupiterVersion}"
    testImplementation "org.assertj:assertj-core:${project.assertjVersion}"

    // Lombok
    compileOnly "org.projectlombok:lombok:${project.lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${project.lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${project.lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${project.lombokVersion}"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

test {
    useJUnitPlatform()
}

shadowJar {
//    enableRelocation true
//    relocationPrefix "me.eyetealer.mckingofthehill.externals"
}
